

/*取得物件的立方股 --lv1--lp0--rc0
	3√(x2+y2+c2)
	*/
global proc float getObjBBCR(){
	float $bbCR;
	$bbs=` exactWorldBoundingBox `;
	$bbX=$bbs[3]-$bbs[0];
	$bbY=$bbs[4]-$bbs[1];
	$bbZ=$bbs[5]-$bbs[2];
	$bbCR=getCubeRoot($bbX,$bbY,$bbZ);
	return $bbCR;
	}

/*基本處理 --lv3--lp0--rc0
	$w:控制器大小比值:預設0.4
	*/	
global proc baseDeal(float $w){
	global string $title;
	global string $capital;
	//select model by your hand first
	string $model[] = `ls -sl`;
	if (`size($model)`==0){
	    print("請選個東西吧");
	}else{
		//get obj boundingbox cube root
		$bbCR=getObjBBCR(); //--lv2--lp0--rc0
		//defind the name

		string $result = `promptDialog
			-title "Rename Object"
			-message "Enter Name:"
			-button "OK" -button "Cancel"
			-defaultButton "OK" -cancelButton "Cancel"
			-dismissString "Cancel"`;
		// Use the command again in query mode to
		// get the text:
		if ($result == "OK") {
			$title = `promptDialog -query -text`;
			}
		$capital = `capitalizeString($title)`;
		        
	    group -n putUnderHigh $model;
	    //set polygon smoothness off
	    string $geo[] = `ls -ni -typ "mesh"` ;
	    select -r $geo;
	    print($geo);
	    displaySmoothness -divisionsU 0 -divisionsV 0 -pointsWire 4 -pointsShaded 1 -polygonObject 1;
	    
	    
	    //create rig
	    select -cl;
	    file -import -type "mayaAscii" -mergeNamespacesOnClash false -rpr "imp_" -options "v=0;"  -pr "U:/04_AfterSchool/Luke/assets/01_kit/move.ma";
	    searchReplaceNames "nameWhat" $title "all";
	    searchReplaceNames "NameWhat" $capital "all";
	    //change ctrl size

		$x=$w*$bbCR;
		select -r gravity.cv[0:11] centerShape.cv[0:11] moveShape.cv[0:10] ;
		scale -r -ocp $x $x $x ;	
		move -a 0 (54.601*$x) 0 smooth_offset;
		scale -r $x $x $x smooth_offset;
	        
	    parent -r $model high;
	    delete putUnderHigh;
	    }
	}

/*去掉nonlinear變形_rig--lv1--lp0--rc0
	*/
global proc deleteDeform(string $capital){
	select -r ("deform"+$capital+"Save_willDEL");
	select -add ("nonlinear"+$capital);
	delete;
	select -r center ;
	setAttr -lock true "center.squash";
	setAttr -lock true "center.bend";
	setAttr -lock true "center.bend_rY";
	setAttr -lock true "center.bend_addRange";
	setAttr -lock true "center.bend_autoBound_bySquash";
	}
	
/*做smooth */
global proc setSmooth(){
	if(!`objExists "smooth_set"`){
		print("沒有smooth_set");
	}else{
		select -r -ne "smooth_set" ;
		string $sels[]=`ls -sl` ;
		for($sel in $sels){
			select -r $sel ;
			displaySmoothness -divisionsU 0 -divisionsV 0 -pointsWire 4 -pointsShaded 1 -polygonObject 1;
			polyPerformAction "polySmooth  -mth 0 -dv 1 -peh 0 -c 1 -kb 0 -ksb 1 -khe 0 -kt 1 -kmb 1 -suv 1 -sl 1 -dpe 1 -ps 0.1 -ro 1" f 0 ;
			}
		smoothConnect();
		}
	}

/*create set --lv1--lp0--rc0
	$typ:0(smooth_set);1(model_set);1(ctrl_set);
	*/
global proc crSet(int $typ){
	$sels=`ls -sl`;
	string $set;
	
	if(`size($sels)`>0){
		if($typ==0)
			$set="smooth_set";
		else if($typ==1)
			$set="model_set"	;
		else
			$set="ctrl_set"	;
		
		if(`objExists $set`)
			sets -add $set $sels;
		else
			sets -n $set $sels;
		}
	}	

/*批次加減Set --lv1--lp1--rc0
	將選擇的物件們,丟到最後一個選擇的set裡
	$typ:0(add);1(remove);	
	*/
global proc batchAddRemoveSet(int $typ){
	string $sels[]=`ls -sl`;
	int $times=`size $sels`-1;
	for($i=0;$i<$times;$i++){
		if ($typ==0)
			sets -include $sels[$times] $sels[$i] ;
		else 
			sets -remove $sels[$times] $sels[$i] ;
		}
	}

/*save as XXX_rig --lv1--lp0--rc0
	*/
global proc saveAsRig(string $title){

	string $path = `file -q -sn`;
	string $dir = `match "^.*/" $path`;
	string $rig = "_rig.ma";
	string $fileName = $dir+"/"+$title+$rig;
	file -rename $fileName; 
	file -save -type "mayaAscii";
	}
	


/*export Hi or Lo --lv1--lp0--rc0
	$typ:0(exportHi);1(exportLo);
	*/
global proc exportHiLo(int $typ){
	string $exportTypes[] = `file -q -typ -sn`;
	string $optionsString = `file -q -op -sn`;
	string $path = `file -q -sn`;
	string $dir = `match "^.*/" $path`;
	string $theFile; 
	
	string $temp = `match "[^/\\]*$" $path`;
	string $title=startString($temp,`sizeBytes $temp`-7);	
	
	if($typ==0){
		$theFile = ($dir+"/"+$title+".ma");	
		select -r  "outputHi_DEL_set";
		delete;
		select -r -ne $title ($title+"_sets");
		print($title+"_sets");
	}else{
		$theFile = ($dir+"/"+$title+"_lowest.ma");
		select -r  "outputLo_DEL_set";	
		delete;		
		select -r -ne $title ($title+"_sets");
		}		
	file -typ $exportTypes[0] -op $optionsString -es $theFile;	
	
	}

/*產生move控制器--lv4--lp1--rc0
*/	
global proc createMove () {
	string $newName;
	string $objMove;
	string $objMoveOffset;	
	string $sels[]=`ls -sl`;
	string $search[]=`ls "move_Grp"`;
	if(`size $search `==0)
		createNode "transform" -name "move_Grp";
	$curve=`curve -n "settingCtrl_Cube" -d 1 -p -1 2 1 -p -1 2 -1 -p 1 2 -1 -p 1 2 1 -p -1 2 1 -p -1 0 1 -p -1 0 -1 -p -1 2 -1 -p -1 2 1 -p -1 0 1 -p 1 0 1 -p 1 2 1 -p 1 2 -1 -p 1 0 -1 -p 1 0 1 -p 1 0 -1 -p -1 0 -1 -k 0 -k 1 -k 2 -k 3 -k 4 -k 5 -k 6 -k 7 -k 8 -k 9 -k 10 -k 11 -k 12 -k 13 -k 14 -k 15 -k 16`;
	setAttr ($curve+".overrideEnabled") 1;
	setAttr ($curve+".overrideColor") 13;

	for ($sel in $sels){
		duplicate -rr "settingCtrl_Cube";
		if(`gmatch $sel "*_offset"`==1)
			$newName =startString($sel,`sizeBytes $sel`-7);
		else
			$newName =$sel;
		$objMove=($newName+"_move");			
		rename "settingCtrl_Cube1"  $objMove;
		$objMoveOffset=($objMove+"_offset");
		string $nowSels_move[]=`ls -sl`;
		
		createNode "transform" -name $objMoveOffset;
		parent $objMove $objMoveOffset ;
		
		$pc=`parentConstraint -weight 1 $sel $objMoveOffset`;
		$sc=`scaleConstraint -offset 1 1 1 -weight 1 $sel $objMoveOffset`;
		delete $pc $sc; 
		
		parentConstraint -mo -weight 1 $objMove $sel;
		scaleConstraint -offset 1 1 1 -weight 1 $objMove $sel;
		
		parent $objMoveOffset "move_Grp" ;
		
		select -r $sel;
		pickWalk -d down;
		string $nowSels[]=`ls -sl`;
		if(`gmatch $nowSels[0]  "*:*Shape*"`)
			HideSelectedObjects;
		
		//改move size
		$moveEdges=getMoveShpScaleValue($sel);//--lv3--lp0--rc0
		scale -r -ocp $moveEdges[0] $moveEdges[1] $moveEdges[2] ($objMove+".cv[0:16]");		
		}
	delete  settingCtrl_Cube ;
	select -cl;
	for($sel in $sels){
		if(`gmatch $sel "*_offset"`==1){
			select -add (startString($sel,`sizeBytes $sel`-7)+"_move");
		}else{
			select -add ($sel+"_move") ;
			}
		}
	}

/*計算move的size--lv3--lp0--rc0
*/
global proc float[] getMoveShpScaleValue(string $obj){	
	float $scale[];
	float $bbe[];
	$baseSR=getSqRoot(2,2);//--lv2--lp0--rc0

	$bbs=` exactWorldBoundingBox $obj`;
	$bbe[0]=$bbs[3]-$bbs[0];
	$bbe[1]=$bbs[4]-$bbs[1];
	$bbe[2]=$bbs[5]-$bbs[2];
	
	if ($bbe[0]==0){
		$temp=getSqRoot($bbe[1],$bbe[2]);//--lv2--lp0--rc0
		$scale[0]=(0.2*$temp/$baseSR);
	}else{	
		$scale[0]=1.2*$bbe[0]/2;
		}
	if ($bbe[1]==0){
		$temp=getSqRoot($bbe[0],$bbe[2]);//--lv2--lp0--rc0
		$scale[1]=(0.2*$temp/$baseSR);
	}else{	
		$scale[1]=0.2*$bbe[1]/2;
		}		
	if ($bbe[2]==0){
		$temp=getSqRoot($bbe[1],$bbe[0]);//--lv2--lp0--rc0
		$scale[2]=(0.2*$temp/$baseSR);
	}else{	
		$scale[2]=1.2*$bbe[2]/2;
		}		
	return $scale;
	}

/*快拍 --lv1
	用playblast快速擷取一張jpg
	*/
global proc snapshot(){
	string $sn = `file -q -sn`;
	string $dir = `match "^.*/" $sn`;
	string $fileN = `file -q -sn -shn`;
	playblast  -fr 1 -format image -cf ($dir+$fileN+".jpg") -sequenceTime 0 -clearCache 1 -viewer 1 -showOrnaments 1 -percent 100 -compression "jpg" -quality 100 -widthHeight 960 540;
	}	


global proc OldSnapshot(string $fileDir){
	$jpgDir=startString($fileDir,`sizeBytes $fileDir`-3);	
	/*
	file -f -options "v=0;"  -typ "mayaAscii" -o $fileDir;
	addRecentFile($fileDir, "mayaAscii");	
	select -r model;
	
	DisplayShadedAndTextured;
	updateModelPanelBar "MainPane*";
	dR_setModelEditorTypes;		
	
	FrameSelectedWithoutChildren;
	fitPanel -selectedNoChildren;
	
	

	refresh -f;
	setAttr "defaultRenderGlobals.imageFormat" 8;	
	setAttr "defaultResolution.width" 1280;
	setAttr "defaultResolution.height" 720;
	
	RenderIntoNewWindow;	
	renderWindowEditor  -e  -wi ($jpgDir+"_render") "renderView";
	*/
	//pause -seconds 5; 
	select -cl;
	refresh -cv -fe "jpg" -fn ($jpgDir+".jpg");
	//playblast  -format image -filename $jpgDir -st 1 -et 1 -clearCache 1 -viewer 1 -showOrnaments 1 -offScreen  -fp 4 -percent 100 -compression "jpg" -quality 100 -widthHeight 1280 720;	
	}
